# 7.3.2. Single-Quoted Style

The *single-quoted style* is specified by surrounding " `'` " indicators. Therefore, within a single-quoted scalar, such characters need to be repeated. This is the only form of *escaping* performed in single-quoted scalars. In particular, the " `\` " and " `"` " characters may be freely used. This restricts single-quoted scalars to [printable](https://yaml.org/spec/1.2.2/#character-set) characters. In addition, it is only possible to break a long single-quoted line where a [space](https://yaml.org/spec/1.2.2/#white-space-characters) character is surrounded by non- [spaces](https://yaml.org/spec/1.2.2/#white-space-characters).

```
[117] c-quoted-quote ::= "''"
```
```
[118] nb-single-char ::=
    c-quoted-quote
  | (
        nb-json
      - c-single-quote    # "'"
    )
```
```
[119] ns-single-char ::=
  nb-single-char - s-white
```

**Example 7.7 Single Quoted Characters**

| ``` 'here''s to "quotes"' ``` | ```json "here's to \"quotes\"" ``` |
| --- | --- |

**Legend:**

- `[c-quoted-quote](https://yaml.org/spec/1.2.2/#rule-c-quoted-quote)`

Single-quoted scalars are restricted to a single line when contained inside a [implicit key](https://yaml.org/spec/1.2.2/#example-single-pair-explicit-entry).

```
[120] c-single-quoted(n,c) ::=
  c-single-quote    # "'"
  nb-single-text(n,c)
  c-single-quote    # "'"
```
```
[121]
nb-single-text(FLOW-OUT)  ::= nb-single-multi-line(n)
nb-single-text(FLOW-IN)   ::= nb-single-multi-line(n)
nb-single-text(BLOCK-KEY) ::= nb-single-one-line
nb-single-text(FLOW-KEY)  ::= nb-single-one-line
```
```
[122] nb-single-one-line ::=
  nb-single-char*
```

**Example 7.8 Single Quoted Implicit Keys**

| ``` 'implicit block key' : [   'implicit flow key' : value,  ] ``` | ```json { "implicit block key":   [ { "implicit flow key": "value" } ] } ``` |
| --- | --- |

**Legend:**

- `[nb-single-one-line](https://yaml.org/spec/1.2.2/#rule-nb-single-one-line)`
- `[c-single-quoted(n,c)](https://yaml.org/spec/1.2.2/#rule-c-single-quoted)`

All leading and trailing [white space](https://yaml.org/spec/1.2.2/#white-space-characters) characters are excluded from the [content](https://yaml.org/spec/1.2.2/#nodes). Each continuation line must therefore contain at least one non- [space](https://yaml.org/spec/1.2.2/#white-space-characters) character. Empty lines, if any, are consumed as part of the [line folding](https://yaml.org/spec/1.2.2/#line-folding).

```
[123] nb-ns-single-in-line ::=
  (
    s-white*
    ns-single-char
  )*
```
```
[124] s-single-next-line(n) ::=
  s-flow-folded(n)
  (
    ns-single-char
    nb-ns-single-in-line
    (
        s-single-next-line(n)
      | s-white*
    )
  )?
```
```
[125] nb-single-multi-line(n) ::=
  nb-ns-single-in-line
  (
      s-single-next-line(n)
    | s-white*
  )
```

**Example 7.9 Single Quoted Lines**

| ``` '·1st non-empty↓ ↓ ·2nd non-empty· →3rd non-empty·' ``` | ```json " 1st non-empty\n2nd non-empty 3rd non-empty " ``` |
| --- | --- |

**Legend:**

- `[nb-ns-single-in-line(n)](https://yaml.org/spec/1.2.2/#rule-nb-ns-single-in-line)`
- `[s-single-next-line(n)](https://yaml.org/spec/1.2.2/#rule-s-single-next-line)`