# 7.3.1. Double-Quoted Style

The *double-quoted style* is specified by surrounding " `"` " indicators. This is the only [style](https://yaml.org/spec/1.2.2/#node-styles) capable of expressing arbitrary strings, by using " `\` " [escape sequences](https://yaml.org/spec/1.2.2/#escaped-characters). This comes at the cost of having to escape the " `\` " and " `"` " characters.

```
[107] nb-double-char ::=
    c-ns-esc-char
  | (
        nb-json
      - c-escape          # '\'
      - c-double-quote    # '"'
    )
```
```
[108] ns-double-char ::=
  nb-double-char - s-white
```

Double-quoted scalars are restricted to a single line when contained inside an [implicit key](https://yaml.org/spec/1.2.2/#example-single-pair-explicit-entry).

```
[109] c-double-quoted(n,c) ::=
  c-double-quote         # '"'
  nb-double-text(n,c)
  c-double-quote         # '"'
```
```
[110]
nb-double-text(n,FLOW-OUT)  ::= nb-double-multi-line(n)
nb-double-text(n,FLOW-IN)   ::= nb-double-multi-line(n)
nb-double-text(n,BLOCK-KEY) ::= nb-double-one-line
nb-double-text(n,FLOW-KEY)  ::= nb-double-one-line
```
```
[111] nb-double-one-line ::=
  nb-double-char*
```

**Example 7.4 Double Quoted Implicit Keys**

| ``` "implicit block key" : [   "implicit flow key" : value,  ] ``` | ```json { "implicit block key":   [ { "implicit flow key": "value" } ] } ``` |
| --- | --- |

**Legend:**

- `[nb-double-one-line](https://yaml.org/spec/1.2.2/#rule-nb-double-one-line)`
- `[c-double-quoted(n,c)](https://yaml.org/spec/1.2.2/#rule-c-double-quoted)`

In a multi-line double-quoted scalar, [line breaks](https://yaml.org/spec/1.2.2/#line-break-characters) are subject to [flow line folding](https://yaml.org/spec/1.2.2/#flow-folding), which discards any trailing [white space](https://yaml.org/spec/1.2.2/#white-space-characters) characters. It is also possible to *escape* the [line break](https://yaml.org/spec/1.2.2/#line-break-characters) character. In this case, the escaped [line break](https://yaml.org/spec/1.2.2/#line-break-characters) is excluded from the [content](https://yaml.org/spec/1.2.2/#nodes) and any trailing [white space](https://yaml.org/spec/1.2.2/#white-space-characters) characters that precede the escaped line break are preserved. Combined with the ability to [escape](https://yaml.org/spec/1.2.2/#escaped-characters) [white space](https://yaml.org/spec/1.2.2/#white-space-characters) characters, this allows double-quoted lines to be broken at arbitrary positions.

```
[112] s-double-escaped(n) ::=
  s-white*
  c-escape         # '\'
  b-non-content
  l-empty(n,FLOW-IN)*
  s-flow-line-prefix(n)
```
```
[113] s-double-break(n) ::=
    s-double-escaped(n)
  | s-flow-folded(n)
```

**Example 7.5 Double Quoted Line Breaks**

| ``` "folded·↓ to a space,→↓ ·↓ to a line feed, or·→\↓ ·\·→non-content" ``` | ```json "folded to a space,\nto a line feed, or \t \tnon-content" ``` |
| --- | --- |

**Legend:**

- `[s-flow-folded(n)](https://yaml.org/spec/1.2.2/#rule-s-flow-folded)`
- `[s-double-escaped(n)](https://yaml.org/spec/1.2.2/#rule-s-double-escaped)`

All leading and trailing [white space](https://yaml.org/spec/1.2.2/#white-space-characters) characters on each line are excluded from the [content](https://yaml.org/spec/1.2.2/#nodes). Each continuation line must therefore contain at least one non- [space](https://yaml.org/spec/1.2.2/#white-space-characters) character. Empty lines, if any, are consumed as part of the [line folding](https://yaml.org/spec/1.2.2/#line-folding).

```
[114] nb-ns-double-in-line ::=
  (
    s-white*
    ns-double-char
  )*
```
```
[115] s-double-next-line(n) ::=
  s-double-break(n)
  (
    ns-double-char nb-ns-double-in-line
    (
        s-double-next-line(n)
      | s-white*
    )
  )?
```
```
[116] nb-double-multi-line(n) ::=
  nb-ns-double-in-line
  (
      s-double-next-line(n)
    | s-white*
  )
```

**Example 7.6 Double Quoted Lines**

| ``` "·1st non-empty↓ ↓ ·2nd non-empty· →3rd non-empty·" ``` | ```json " 1st non-empty\n2nd non-empty 3rd non-empty " ``` |
| --- | --- |

**Legend:**

- `[nb-ns-double-in-line](https://yaml.org/spec/1.2.2/#rule-nb-ns-double-in-line)`
- `[s-double-next-line(n)](https://yaml.org/spec/1.2.2/#rule-s-double-next-line)`